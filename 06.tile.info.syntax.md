#### Tile Info Syntax

~~~~~
tile_info ( ) {
    minLog2TileCols = tile_log2(MAX_TILE_WIDTH_SB, MaxSbCols)
    maxLog2TileCols = tile_log2(1, Min(MaxSbCols, MAX_TILE_COLS))
    maxLog2TileRows = tile_log2(1, Min(MaxSbRows, MAX_TILE_ROWS))
    minLog2Tiles = Max(minLog2TileCols, 
                       tile_log2(MAX_TILE_AREA_SB, MaxSbRows * MaxSbCols))
                       
    @@uniform_tile_spacing_flag                                                f(1)
    if ( uniform_tile_spacing_flag ) {
        TileColsLog2 = minLog2TileCols
        while ( TileColsLog2 < maxLog2TileCols ) {
            @@increment_tile_cols_log2                                         f(1)
            if ( increment_tile_cols_log2 == 1 )
                TileColsLog2++
            else
                break
        }
        sizeSb = (MaxSbCols + (1 << TileColsLog2) - 1) >> TileColsLog2
        i = 0
        for (startSb = 0; startSb < MaxSbCols; startSb += sizeSb) {
          MiColStarts[ i ] = startSb << (MAX_SB_SIZE_LOG2 - MI_SIZE_LOG2)
          i += 1
        }
        MiColStarts[i] = MiCols
        TileCols = i
        
        minLog2TileRows = Max( minLog2Tiles - TileColsLog2, 0)
        maxTileHeightSb = MaxSbRows >> minLog2TileRows
        TileRowsLog2 = minLog2TileRows
        while ( TileRowsLog2 < maxLog2TileRows ) {
            @@increment_tile_rows_log2                                         f(1)
            if ( increment_tile_rows_log2 == 1 )
                TileRowsLog2++
            else
                break
        }
        sizeSb = (MaxSbCols + (1 << TileRowsLog2) - 1) >> TileRowsLog2
        i = 0
        for (startSb = 0; startSb < MaxSbRows; startSb += sizeSb) {
          MiRowStarts[ i ] = startSb << (MAX_SB_SIZE_LOG2 - MI_SIZE_LOG2)
          i += 1
        }
        MiRowStarts[i] = MiRows
        TileRows = i
    } else {
        widestTileSb = 0
        startSb = 0
        for ( i = 0; startSb < MaxSbCols && i < MAX_TILE_COLS; i++ ) {
            MiColStarts[ i ] = startSb << (MAX_SB_SIZE_LOG2 - MI_SIZE_LOG2)
            maxWidth = Min(MaxSbCols - startSb, MAX_TILE_WIDTH_SB)
            sizeSb = decode_uniform(maxWidth) + 1
            widestTileSb = Max( sizeSb, widestTileSb )
            startSb += sizeSb
        }
        MiColStarts[i] = MiCols
        TileCols = i
        TileColsLog2 = tile_log2(1, TileCols)

        if ( minLog2Tiles > 0 )
            maxTileAreaSb = (MaxSbRows * MaxSbCols) >> (minLog2Tiles + 1)
        else
            maxTileAreaSb = MaxSbRows * MaxSbCols
        maxTileHeightSb = Max( maxTileAreaSb / widestTileSb, 1 )

        startSb = 0
        for ( i = 0; startSb < MaxSbRows && i < MAX_TILE_ROWS; i++ ) {
            MiRowStarts[ i ] = startSb << (MAX_SB_SIZE_LOG2 - MI_SIZE_LOG2)
            maxHeight = Min(MaxSbRows - startSb, maxTileHeightSb)
            sizeSb = decode_uniform(maxHeight) + 1
            startSb += sizeSb
        }
        MiRowStarts[ i ] = mi_rows
        TileRows = i
        TileRowsLog2 = tile_log2(1, TileRows)
    }
    startMi = 0
    maxTileHeightMi = maxTileHeightSb << (MAX_SB_SIZE_LOG2 - MI_SIZE_LOG2)
    for (i = 0; i < TileRows; i++) {
      if (MiRowStarts[i+1] - startMi > maxTileHeightMi) {
        AllowDependentTileRow[i] = 0
        startMi = MiRowStarts[i+1]
      } else {
        AllowDependentTileRow[i] = 1
      }
    }
    if ( TileRowsLog2 > 0 )
        @@dependent_tiles                                                      f(1)
    else
        dependent_tiles = 0
    if ( TileRowsLog2 > 0 || TileColsLog2 > 0 )
        @@loop_filter_across_tiles                                             f(1)
    else
        loop_filter_across_tiles = 0
    @@tile_size_bytes_minus_1                                                  f(2)
    TileSizeBytes = tile_size_bytes_minus_1 + 1
}
~~~~~
{:.syntax }
